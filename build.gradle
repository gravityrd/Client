plugins {
    id 'java'
    id 'signing'
    id 'maven'
}

group = "com.gravityrd"
description = """java client to interact with the Gravity recommendation engine"""
version = '1.4.1'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.7
targetCompatibility = 1.7
tasks.withType(JavaCompile) { options.incremental = true }

repositories {
    maven {
        url "https://oss.sonatype.org/content/groups/public"
    }
    mavenCentral()
    jcenter()
    maven {
        url "http://artifactory.int.gravityrd.com/artifactory/repo"
    }
}

dependencies {
    compile 'com.gravityrd:jsondto:1.3.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.0'
    compile 'commons-codec:commons-codec:1.10'
    testCompile 'junit:junit:4.12'
}

javadoc {
	if (JavaVersion.current().isJava8Compatible()) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar, jar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {

                name 'java-client'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Gravity Java client to access recommendation engine.'
                url 'https://gravityrd.com'

                scm {
                    connection 'https://github.com/gravityrd/java-client'
                    developerConnection 'git@github.com:gravityrd/java-client.git'
                    url 'https://github.com/gravityrd/java-client.git'
                }

                licenses {
                    license {
                        name 'GNU General Public License, version 2'
                        url 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'gravityrd'
                        name 'Gravity R&D'
                        email 'support@gravityrd.com'
                    }
                }
            }
        }
    }
}
